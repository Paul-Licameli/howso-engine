(seq
	#unit_test (direct_assign_to_entities (assoc unit_test (load "../unit_tests/unit_test.amlg")))
	(call (load "../unit_tests/unit_test_howso.amlg") (assoc name "at_prnn_fglass.amlg"))

	(call_entity "howso" "create_trainee" (assoc trainee "model" ))
	(assign_to_entities "howso" (assoc trainee "model"))
	;(call_entity "howso" "set_random_seed" (assoc trainee "model" seed "12345"))

	(declare (assoc dataset (load "datasets/prnn_fglass.csv")))
	(declare (assoc
		features (first dataset)
		data (tail dataset)
	))
	(declare (assoc
		twenty_percent_size (* (size data) .2)
		eighty_percent_size (* (size data) .8)
	))
	(declare (assoc
		;randomly select 20% unique indices to holdout
		holdout_indices (rand (indices data) twenty_percent_size (true))
	))
	(declare (assoc
		test_data (unzip data holdout_indices)
		train_data (filter (lambda (not (contains_value holdout_indices (target_index)))) data)
	))

	(call_entity "howso" "set_feature_attributes" (assoc
		trainee "model"
		features (assoc "target" (assoc "type" "nominal" "data_type" "number"))
	))

	(call_entity "howso" "train" (assoc
		trainee "model"
		features features
		input_cases train_data
	))

	(print "loaded: " (get (call_entity "howso" "get_num_training_cases" (assoc trainee "model")) (list "payload" "count")) "\n")

	(call_entity "howso" "analyze" (assoc
		trainee "model"
		context_features (trunc features)
		action_features (list (last features))
		k_folds 1
		num_analysis_samples 1000
		use_deviations (null)
	))

	(declare (assoc
		internal_params (call_entity "howso" "get_internal_parameters" (assoc trainee "model"))
	))
	(declare (assoc
		analyze_mae (get internal_params (list "payload" "hyperparameter_map" "target" "full" ".none" "gridSearchError"))
	))

;	(print "\nInternal Params: " internal_params )

	(declare (assoc num_correct 0))
	(map
		(lambda (let
			(assoc
				result
					(call_entity "howso" "react" (assoc
						context_features (trunc features)
						action_features (list (last features))
						context_values (trunc (target_value 2))
					))
			)
			;(print (get result (list "action_values" 0)) " vs " (last (target_value)) "\n")

			(if (= (get result (list "payload" "action_values" 0)) (last (target_value)))
				(accum (assoc num_correct 1))
			)
		))
		test_data
	)
	(declare (assoc correct (/ num_correct (size test_data))))

	(print "ANALYZED TO USE DEVIATIONS: ")
	(call assert_true (assoc obs (get internal_params (list "payload" "hyperparameter_map" "target" "full" ".none" "useDeviations")) ))

	(print "Analyze MAE: 0.35 > " analyze_mae " ")
	(call assert_true (assoc obs (< analyze_mae 0.35)))

	(print "Accuracy over 60%% : " correct  " ")
	(call assert_true (assoc obs (>= correct 0.60)))


	(call exit_if_failures (assoc msg unit_test_name ))
)